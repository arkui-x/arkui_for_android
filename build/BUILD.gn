# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/interfaces/napi/kits/napi_lib.gni")

# libace target
ohos_shared_library("libace_android") {
  deps = [
    "$ace_root/build:libace_static_android",
    "$ace_root/interfaces/native:ace_packages_ndk",
  ]
  part_name = "ace_engine_cross"
  subsystem_name = "arkui"
}

java_library("ace_dev_java") {
  java_files = [
    "$flutter_root_new/engine/ace_shell/test/java/ohos/ace/adapter/AceActivity.java",
    "$flutter_root_new/engine/ace_shell/test/java/ohos/ace/adapter/AceViewAosp.java",
    "$flutter_root_new/engine/ace_shell/test/java/ohos/ace/adapter/AceVsyncWaiter.java",
    "$flutter_root_new/engine/ace_shell/test/java/ohos/ace/adapter/LibraryLoader.java",
  ]

  subsystem_name = "arkui"
  part_name = "ace_engine_cross"
}

ohos_source_set("ace_dev_test") {
  sources = [
    "$flutter_root_new/engine/ace_shell/test/jni/flutter_ace_view_jni.cpp",
    "$flutter_root_new/engine/ace_shell/test/jni/flutter_ace_view.cpp",
    "$flutter_root_new/engine/ace_shell/test/jni/flutter_window.cpp",
    "$flutter_root_new/engine/ace_shell/test/jni/jni_load.cpp",
    "$flutter_root_new/engine/ace_shell/test/jni/jni_registry.cpp",
  ]

  deps = [
    "$ace_flutter_engine_root_new:third_party_flutter_engine_dev_android",
  ]
}

ohos_shared_library("libace_dev") {
  deps = [
    ":ace_dev_test",
    ":ace_dev_java",
    "$ace_flutter_engine_root_new:third_party_flutter_engine_dev_android",
  ]
  subsystem_name = "arkui"
  part_name = "ace_engine_cross"
}

# Install final jar
ohos_combine_jars("ace_android_adapter_java") {
  deps = [
    "$ace_root/adapter/android/entrance/java:ace_platform_android_java",
    "$ace_root/adapter/android/entrance/java:ace_platform_common_java",
  ]

  part_name = "ace_engine_cross"
  subsystem_name = "arkui"
  jar_path = "${root_out_dir}/ace_android_adapter.jar"
}

common_napi_modules = []
foreach(module, common_napi_libs) {
  ace_napi_lib(module) {
    platform = "android"
  }
  common_napi_modules += [ ":$module" ]
}

# Install packages
group("ace_packages") {
  deps = [
    ":ace_android_adapter_java",
    ":libace_android",
    ":libace_dev",
  ]

  deps += common_napi_modules
}
